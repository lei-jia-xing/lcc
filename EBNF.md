- 编译单元 CompUnit → {Decl} {FuncDef} MainFuncDef // 1.是否存在Decl 2.是否存在FuncDef
- 声明 Decl → ConstDecl | VarDecl // 覆盖两种声明
- 常量声明 ConstDecl → 'const' BType ConstDef { ',' ConstDef } ';' // 1.花括号内重复0次 2.花括号内重复多次
- 基本类型 BType → 'int'
- 常量定义 ConstDef → Ident [ '[' ConstExp ']' ] '=' ConstInitVal // 包含普通变量、一维数组两种情况
- 常量初值 ConstInitVal → ConstExp | '{' [ ConstExp { ',' ConstExp } ] '}' // 1.常表达式初值 2.一维数组初值
- 变量声明 VarDecl → [ 'static' ] BType VarDef { ',' VarDef } ';' // 1.花括号内重复0次 2.花括号内重复多次
- 变量定义 VarDef → Ident [ '[' ConstExp ']' ] | Ident [ '[' ConstExp ']' ] '=' InitVal //包含普通常量、一维数组定义
- 变量初值 InitVal → Exp | '{' [ Exp { ',' Exp } ] '}' // 1.表达式初值 2.一维数组初值
- 函数定义 FuncDef → FuncType Ident '(' [FuncFParams] ')' Block // 1.无形参 2.有形参
- 主函数定义 MainFuncDef → 'int' 'main' '(' ')' Block // 存在main函数
- 函数类型 FuncType → 'void' | 'int' // 覆盖两种类型的函数
- 函数形参表 FuncFParams → FuncFParam { ',' FuncFParam } // 1.花括号内重复0次 2.花括号内重复多次
- 函数形参 FuncFParam → BType Ident ['[' ']'] // 1.普通变量 2.一维数组变量
- 语句块 Block → '{' { BlockItem } '}' // 1.花括号内重复0次 2.花括号内重复多次
- 语句块项 BlockItem → Decl | Stmt // 覆盖两种语句块项
- 语句 Stmt → LVal '=' Exp ';' // 每种类型的语句都要覆盖
  - | [Exp] ';' // 有无Exp两种情况；printf函数调用
  - | Block
  - | 'if' '(' Cond ')' Stmt [ 'else' Stmt ] // 1.有else 2.无else
  - | 'for' '(' [ForStmt] ';' [Cond] ';' [ForStmt] ')' Stmt // 1. 无缺省，1种情况 2. ForStmt与Cond中缺省一个，3种情况 3. ForStmt与Cond中缺省两个，3种情况 4. ForStmt与Cond全部缺省，1种情况
  - | 'break' ';'
  - | 'continue' ';'
  - | 'return' [Exp] ';' // 1.有Exp 2.无Exp
  - | 'printf''('StringConst {','Exp}')'';' // 1.有Exp 2.无Exp
- 语句 ForStmt → LVal '=' Exp { ',' LVal '=' Exp } // 1.花括号内重复0次 2.花括号内重复多次
- 表达式 Exp → AddExp // 存在即可
- 条件表达式 Cond → LOrExp // 存在即可
- 左值表达式 LVal → Ident ['[' Exp ']'] // 1.普通变量、常量 2.一维数组
- 基本表达式 PrimaryExp → '(' Exp ')' | LVal | Number
- 数值 Number → IntConst // 存在即可
- 一元表达式 UnaryExp → PrimaryExp | Ident '(' [FuncRParams] ')' | UnaryOp UnaryExp // 3种情况均需覆盖,函数调用也需要覆盖FuncRParams的不同情况
- 单目运算符 UnaryOp → '+' | '−' | '!' 注：'!'仅出现在条件表达式中 // 三种均需覆盖
- 函数实参表达式 FuncRParams → Exp { ',' Exp } // 1.花括号内重复0次 2.花括号内重复多次 3.Exp需要覆盖数组传参和部分数组传参
- 乘除模表达式 MulExp → UnaryExp | MulExp ('*' | '/' | '%') UnaryExp // 1.UnaryExp 2.* 3./ 4.% 均需覆盖
- 加减表达式 AddExp → MulExp | AddExp ('+' | '−') MulExp // 1.MulExp 2.+ 需覆盖 3.- 需覆盖
- 关系表达式 RelExp → AddExp | RelExp ('<' | '>' | '<=' | '>=') AddExp // 1.AddExp 2.< 3.> 4.<= 5.>= 均需覆盖
- 相等性表达式 EqExp → RelExp | EqExp ('==' | '!=') RelExp // 1.RelExp 2.== 3.!= 均需覆盖
- 逻辑与表达式 LAndExp → EqExp | LAndExp '&&' EqExp // 1.EqExp 2.&& 均需覆盖
- 逻辑或表达式 LOrExp → LAndExp | LOrExp '||' LAndExp // 1.LAndExp 2.|| 均需覆盖
- 常量表达式 ConstExp → AddExp 注：使用的 Ident 必须是常量 // 存在即可
